apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'groovy'

mainClassName = "se.inera.statistik.tools.UpdateEnhetNamnFromHsaFileService"
applicationDefaultJvmArgs = ["-Dfile.encoding=utf8"]

startScripts {
    doLast {
        // Get hold of the file name of the statistik-service jar file
        def statistikDependency = configurations.runtime.filter { it.name.startsWith("statistik-service") }.singleFile.name
        // Insert our default script parameters into the script templates
        unixScript.text = unixScript.text.replaceAll(/"\$@"$/,
                                                     "--classpath=lib/${statistikDependency} \"\\\$@\"")
        windowsScript.text = windowsScript.text.replaceAll(/%CMD_LINE_ARGS%/,
                                                           "--classpath=lib/${statistikDependency} %CMD_LINE_ARGS%")
    }
}

artifacts {
    archives distZip
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:${groovyAllVersion}"
    compile "org.apache.commons:commons-dbcp2:${commonDCHPVersion}"
    compile "org.codehaus.gpars:gpars:${gparsVersion}"
    compile "joda-time:joda-time:${jodaTimeVersion}"
    compile "org.apache.httpcomponents:httpclient:${httpClientVersion}"
    compile "commons-cli:commons-cli:${commonCliVersion}"

    runtime "mysql:mysql-connector-java:${mysqlConnectorVersion}"
    runtime project(":statistik-service")
}

apply plugin: 'application'

mainClassName = "liquibase.integration.commandline.Main"
applicationDefaultJvmArgs = ["-Dfile.encoding=utf8"]

startScripts {
    doLast {
        // Get hold of the file name of the statistik-service jar file
        def statistikDependency = configurations.runtime.filter { it.name.startsWith("statistik-service") }.singleFile.name
        // Insert our default script parameters into the script templates
        unixScript.text = unixScript.text.replaceAll(/"\$@"$/,
            "--driver=com.mysql.jdbc.Driver --classpath=lib/${statistikDependency} --changeLogFile=changelog/changelog.xml \"\\\$@\"")
        windowsScript.text = windowsScript.text.replaceAll(/%CMD_LINE_ARGS%/,
            "--driver=com.mysql.jdbc.Driver --classpath=lib/${statistikDependency} --changeLogFile=changelog/changelog.xml %CMD_LINE_ARGS%")
    }
}

artifacts {
    archives distZip
}

configurations.runtime {
    transitive = false
}

dependencies {
    runtime project(":statistik-service")
    runtime "org.liquibase:liquibase-core:${liquibaseVersion}"
    runtime "mysql:mysql-connector-java:${mysqlConnectorVersion}"
}

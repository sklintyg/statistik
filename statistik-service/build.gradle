
description = 'Inera Statistics Service - Service Module'

apply plugin: 'groovy'
apply plugin: 'idea'

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test {
        // compile everything in src/test with groovy (enables cross-compiling)
        java { srcDirs = [] }
        groovy { srcDirs = ["src/test/groovy", "src/test/java"] }
    }
}

afterEvaluate {
    idea {
        module.scopes.PROVIDED.plus += [configurations.provided] // Adds the provided configuration to IntelliJs provided scope
    }
}

// JPA expects classes and configuration files to be in the same directory.
sourceSets.main.output.resourcesDir = sourceSets.main.output.classesDir
// B/c of the above, we create duplicates within the JAR. This configuration excludes the duplicates.
jar.duplicatesStrategy = DuplicatesStrategy.EXCLUDE

dependencies {
    compile project(':hsa-integration')

    compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:${hibernateJpaVersion}"
    compile "org.hibernate:hibernate-entitymanager:${hibernateVersion}"
    compile "org.springframework:spring-orm:${springContextVersion}"
    compile "org.springframework:spring-jms:${springContextVersion}"
    compile "org.liquibase:liquibase-core:${liquibaseVersion}"
    compile "se.riv.clinicalprocess.healthcond.certificate:clinicalprocess-healthcond-certificate-schemas:${certificateSchemaVersion}"
    compile "se.inera.intyg.common:logging-util:${intygCommonLoggingVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonJavaTimeVersion}"
    compile "se.inera.intyg.common:hsa-integration:${intygCommonVersion}"
    compile "com.helger:ph-schematron:${schematronVersion}"
    compile "se.inera.intyg.clinicalprocess.healthcond.certificate:intyg-clinicalprocess-healthcond-certificate-schematron:${intygSchematronVersion}"

    testCompile "org.codehaus.groovy:groovy-all:${groovyAllVersion}"
    testCompile "org.apache.activemq:activemq-spring:${activeMqVersion}"

    testCompile "org.springframework:spring-aop:${springContextVersion}"
    testCompile "org.aspectj:aspectjrt:1.8.9"
    testCompile "org.aspectj:aspectjweaver:1.8.9"

    testRuntime "com.h2database:h2:${h2Version}"

    provided "org.apache.geronimo.specs:geronimo-jms_1.1_spec:${jmsVersion}"
}

description = 'Inera Statistics Service - Service Module'

buildscript {
    dependencies {
        // This plugin adds a "provided" scope to the project
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.+'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'provided-base'

sourceSets {
    test {
        // compile everything in src/test with groovy (enables cross-compiling)
        java { srcDirs = [] }
        groovy { srcDirs = ["src/test/groovy", "src/test/java"] }
    }
}

// JPA expects classes and configuration files to be in the same directory.
sourceSets.main.output.resourcesDir = sourceSets.main.output.classesDir
// B/c of the above, we create duplicates within the JAR. This configuration excludes the duplicates.
jar.duplicatesStrategy = DuplicatesStrategy.EXCLUDE

dependencies {
    compile project(':hsa-integration')

    compile "org.apache.cxf:cxf-rt-transports-http-jetty:${cxfVersion}"
    compile "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final"
    compile "org.hibernate:hibernate-entitymanager:4.3.1.Final"
    compile "org.springframework:spring-orm:${springContextVersion}"
    compile "org.springframework:spring-jms:${springContextVersion}"
    compile "org.liquibase:liquibase-core:${liquibaseVersion}"

    testCompile 'org.codehaus.groovy:groovy-all:2.3.6'
    testCompile "org.apache.activemq:activemq-spring:5.8.0"

    testRuntime "com.h2database:h2:1.3.171"

    provided "org.apache.geronimo.specs:geronimo-jms_1.1_spec:1.1.1"
}

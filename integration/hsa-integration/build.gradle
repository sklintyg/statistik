ext.wsdlFile = file("src/main/resources/schemas/wsdl/HsaWsInteraction_3.18.wsdl")
ext.xsdFile = file("src/main/resources/schemas/schemas/HsaWsResponder_3.31.1.xsd")
ext.generatedJavaDir = file("generatedSource/java")

sourceSets.main.java {
    srcDir generatedJavaDir
}

clean.doLast { ant.delete(dir: generatedJavaDir.parent) }

configurations {
    wsdlgen
    xsdgen
}

if (project.hasProperty("codeQuality")) {
    license {
        exclude "**/*.java" // This excludes too many files, but "generatedSource/**/*.java" does not work for some reason.
    }
}

task wsdl2Java(type: JavaExec) {
    inputs.files wsdlFile
    outputs.dir generatedJavaDir
    classpath configurations.wsdlgen
    jvmArgs "-Djavax.xml.accessExternalSchema=all"
    main "org.apache.cxf.tools.wsdlto.WSDLToJava"
    args "-xjc", "-b", "${projectDir}/src/main/resources/jaxb-bindings.xjb", "-validate", "-verbose",
            "-p", "urn:riv:hsa:HsaWs:3=se.inera.ifv.hsaws.v3",
            "-p", "urn:riv:hsa:HsaWsResponder:3=se.inera.ifv.hsawsresponder.v3",
            "-p", "http://www.w3.org/2005/08/addressing=org.w3.wsaddressing10", '-d', generatedJavaDir.path, wsdlFile
}

task xsd2Java() {
    doLast {
        ant.taskdef(
                name: 'xjc',
                classname: 'com.sun.tools.xjc.XJCTask',
                classpath: configurations.xsdgen.asPath
        )
        ant.xjc(
                destdir: generatedJavaDir.path,
                package: 'se.inera.ifv.hsawsresponder.v3._31._1',
                schema: xsdFile
        )
    }
}

compileJava.dependsOn(
        wsdl2Java,
        xsd2Java
)

dependencies {
    compile "se.inera.intyg.infra:hsa-integration:${infraVersion}"

    compile "org.jvnet.jaxb2_commons:jaxb2-basics:${jaxb2Version}"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider"
    compile "org.apache.cxf:cxf-rt-frontend-jaxrs:${cxfVersion}"
    compile "org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}"
    compile "org.springframework:spring-context"
    compile "org.springframework.security:spring-security-web"

    wsdlgen "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:${cxfVersion}"
    wsdlgen "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:${cxfVersion}"
    wsdlgen "org.jvnet.jaxb2_commons:jaxb2-basics:${jaxb2Version}"

    wsdlgen "com.sun.xml.bind:jaxb-xjc:${jaxbXjcVersion}"
    wsdlgen "com.sun.xml.bind:jaxb-core:${jaxbCoreVersion}"
    wsdlgen "javax.xml.bind:jaxb-api:${jaxbImplVersion}"
    wsdlgen "jakarta.xml.ws:jakarta.xml.ws-api"
    wsdlgen "jakarta.annotation:jakarta.annotation-api"
    wsdlgen "ch.qos.logback:logback-classic"

    xsdgen "com.sun.xml.bind:jaxb-xjc:${jaxbXjcVersion}"
    xsdgen "com.sun.xml.bind:jaxb-core:${jaxbCoreVersion}"
    xsdgen "javax.xml.bind:jaxb-api:${jaxbImplVersion}"
}

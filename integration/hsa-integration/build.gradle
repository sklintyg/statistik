description = 'Inera Statistics HSA-Integration module'

/* TODO: clean up file/string */
ext.wsdlFile = file("src/main/resources/schemas/wsdl/HsaWsInteraction_3.18.wsdl")
ext.generatedJavaDir = "build/generatedSource/java"

sourceSets.main.java {
    srcDir file(generatedJavaDir)
}

configurations {
    wsdlgen
}

task wsdl2Java(type: JavaExec) {
    inputs.files wsdlFile
    outputs.dir file(generatedJavaDir)
    classpath configurations.wsdlgen
    main "org.apache.cxf.tools.wsdlto.WSDLToJava"
    args "-xjc-XhashCode", "-xjc-Xequals", "-xjc-XtoString", "-xjc-Xinheritance",
    "-xjc-b,${projectDir}/src/main/resources/jaxb-bindings.xjb", "-validate", "-verbose",
    "-p", "urn:riv:hsa:HsaWs:3=se.inera.ifv.hsaws.v3", 
    "-p", "urn:riv:hsa:HsaWsResponder:3=se.inera.ifv.hsawsresponder.v3", 
    "-p", "http://www.w3.org/2005/08/addressing=org.w3.wsaddressing10", '-d', files(generatedJavaDir).asPath, wsdlFile
}

compileJava.dependsOn(wsdl2Java)

dependencies {
    compile "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4"
    compile "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.2.3"
    compile "org.apache.cxf:cxf-rt-frontend-jaxrs:$cxfVersion"
    compile "org.apache.cxf:cxf-rt-frontend-jaxws:$cxfVersion"
    compile "org.springframework:spring-context:$springContextVersion"
    compile(group: "org.springframework.security", name: "spring-security-web", version:springSecurityVersion) {
        exclude(module: "spring-expression")
        exclude(module: "spring-aop")
        exclude(module: "spring-jdbc")
        exclude(module: "spring-core")
        exclude(module: "spring-beans")
        exclude(module: "spring-tx")
    }

    wsdlgen "org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:$cxfVersion"
    wsdlgen "org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:$cxfVersion"
    wsdlgen "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.4"
}

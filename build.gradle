plugins {
    id "se.inera.intyg.plugin.common" version "1.0.67" apply false
    id "se.inera.intyg.plugin.fitnesse" version "1.0.8" apply false
    id "org.akhikhl.gretty" version "1.4.2" apply false
    id "com.moowork.node" version "1.1.1" apply false
}

ext {
    infraVersion = System.properties['infraVersion'] ?: '3.9.0.+'

    intygSchematronVersion = "1.0.38"
    certificateSchemaVersion = "3.2.0-RC3"
    monitoringSchemaVersion = "1.0.0.0"
    intygTransportstyrelsenSchemasVersion = "1.0.4"

    activeMqVersion = "5.13.5"
    aspectJVersion = "1.8.9"
    commonCliVersion = "1.2"
    commonDCHPVersion = "2.0"
    commonsLang3Version = "3.2.1"
    cxfVersion = '3.1.3'
    googleGuavaVersion = "18.0"
    gparsVersion = "1.1.0"
    groovyAllVersion = "2.3.6"
    groovyHttpVersion = "0.7.1"
    h2Version = "1.4.196"
    hikariCPVersion = "2.3.5"
    hibernateJpaVersion = "1.0.0.Final"
    hibernateVersion = "4.3.1.Final"
    httpClientVersion = "4.2.1"
    httpVersion = "4.4.1"
    jacksonVersion = "2.8.3"
    javaxServletVersion = "3.1.0"
    javaxWsRsApiVersion = "2.0.1"
    jaxb2Version = "0.9.5"
    jmsVersion = "1.1.1"
    liquibaseVersion = "2.0.5"
    logbackVersion = "1.2.3"
    mysqlConnectorVersion = "5.1.46"
    poiVersion = "3.11"
    restAssuredVersion = "2.9.0"
    schematronVersion = "4.1.0"
    slf4jVersion = "1.7.21"
    springContextVersion = "4.3.17.RELEASE"
    springSecuritySamlVersion = "1.0.3.RELEASE"
    springSecurityVersion = "4.2.5.RELEASE"
    springSessionVersion = "1.3.3.RELEASE"
    stringTemplateVersion = "4.0.8"
    tomcatVersion = "7.0.57"
    xalanVersion = "2.7.2"
    xmlApisVersion = "1.4.01"
    zipletVersion = "2.1.2"
    shedlockSpringVersion = "0.18.2"

    fitnesseVersion = "20180127"
    junitVersion = "4.11"
    mockitoVersion = "1.9.5"
    springTestVersion = "4.2.2.RELEASE"
    jettyVersion = "9.4.12.v20180830"
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/repository/releases/" }
        mavenCentral()
    }

    // Findbugs does not work very well with groovy code. Hence we exclude groovy projects.
    ext.findbugsExclude = "(fileservice|.*test)\$"

    project.afterEvaluate {
        tasks.withType(FindBugs.class).forEach { task ->
            task.classes = task.classes.filter { clazz ->
                !clazz.path.matches(".*\\.(xml|sql|json|log|txt|ANS|properties)")
            }
        }
    }

    // Errorprone has no mechanism (yet) for excluding generated code. We therefore disable it for hsa-integration.
    ext.errorproneExclude = "hsa-integration"

    apply plugin: 'maven'
    apply plugin: 'se.inera.intyg.plugin.common'

    dependencies {
        compile "ch.qos.logback:logback-classic:${logbackVersion}"
        compile "com.google.guava:guava:${googleGuavaVersion}"

        testCompile "junit:junit:${junitVersion}"
        testCompile "org.springframework:spring-test:${springTestVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    group = 'se.inera.statistik'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        exclude '**/*FunctionalTest.*'
    }

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.infra'}.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

configure(subprojects.findAll { ['statistik-service', 'web'].contains(it.name) }) {
    test {
        exclude '**/*IT*'
    }

    task integrationTest(type: Test) {
        outputs.upToDateWhen { false }
        include '**/*IT*'
    }
}

apply from: "checkPnr.gradle"

task tagRelease(type: TagReleaseTask)

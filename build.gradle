plugins {
    id "se.inera.intyg.plugin.common" version "2.0.9" apply false
    id "se.inera.intyg.plugin.fitnesse" version "1.0.8" apply false
    id "org.gretty" version "2.3.1" apply false
    id "com.moowork.node" version "1.3.1" apply false
}

ext {
    infraVersion = System.properties['infraVersion'] ?: '3.11.0.+'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    intygSchematronVersion = "1.0.48"
    rivtaClinicalprocessSchemasVersion = "4.0.3"
    internalMonitoringSchemaVersion = "1.0.4"
    intygTransportstyrelsenSchemasVersion = "1.0.5"

    activeMqVersion = "5.13.5"
    aspectJVersion = "1.8.9"
    commonCliVersion = "1.2"
    commonDCHPVersion = "2.0"
    commonsLang3Version = "3.8.1"
    commonsTextVersion = "1.6"
    cxfVersion = '3.2.4'
    googleGuavaVersion = "27.0.1-jre"
    gparsVersion = "1.1.0"
    groovyAllVersion = "2.3.6"
    groovyHttpVersion = "0.7.1"
    h2Version = "1.4.196"
    hikariCPVersion = "2.3.5"
    hibernateJpaVersion = "1.0.0.Final"
    hibernateVersion = "4.3.1.Final"
    httpClientVersion = "4.2.1"
    httpVersion = "4.4.1"
    jacksonVersion = "2.8.3"
    javaxServletVersion = "3.1.0"
    javaxWsRsApiVersion = "2.0.1"
    jaxb2Version = "1.11.1"
    jmsVersion = "1.1.1"
    jsonwebtokenVersion = "0.10.7"
    auth0JwksVersion = "0.8.3"
    liquibaseVersion = "2.0.5"
    logbackVersion = "1.2.3"
    mysqlConnectorVersion = "5.1.46"
    poiVersion = "3.11"
    restAssuredVersion = "2.9.0"
    schematronVersion = "4.1.0"
    slf4jVersion = "1.7.21"
    springContextVersion = "4.3.17.RELEASE"
    springSecuritySamlVersion = "1.0.3.RELEASE"
    springSecurityVersion = "4.2.5.RELEASE"
    springSessionVersion = "1.3.3.RELEASE"
    stringTemplateVersion = "4.0.8"
    tomcatVersion = "7.0.57"
    xalanVersion = "2.7.2"
    xmlApisVersion = "1.4.01"
    zipletVersion = "2.1.2"
    shedlockSpringVersion = "0.18.2"

    fitnesseVersion = "20180127"
    junitVersion = "4.11"
    mockitoVersion = "1.9.5"
    springTestVersion = "4.2.2.RELEASE"
    jettyVersion = "9.4.12.v20180830"

    nodeJsVersion = "10.16.2"
    npmVersion = "6.10.3"
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/repository/public/" }
        jcenter {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    // Spotbugs does not work very well with groovy code. Hence we exclude groovy projects.
    ext.spotbugsExclude = "(fileservice|.*groovy)\$"

    // Errorprone has no mechanism (yet) for excluding generated code. We therefore disable it for hsa-integration.
    ext.errorproneExclude = "hsa-integration"

    apply plugin: 'maven'
    apply plugin: 'se.inera.intyg.plugin.common'

    dependencies {
        compile "ch.qos.logback:logback-classic:${logbackVersion}"
        compile "com.google.guava:guava:${googleGuavaVersion}"

	compile "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.1"
        testCompile "junit:junit:${junitVersion}"
        testCompile "org.springframework:spring-test:${springTestVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
        testCompile "se.inera.intyg.refdata:refdata:${refDataVersion}"
    }

    group = 'se.inera.statistik'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        exclude '**/*FunctionalTest.*'
    }

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.infra'}.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

configure(subprojects.findAll { ['statistik-service', 'web'].contains(it.name) }) {
    test {
        exclude '**/*IT*'
    }

    task integrationTest(type: Test) {
        outputs.upToDateWhen { false }
        include '**/*IT*'
    }
}

apply from: "checkPnr.gradle"

task tagRelease(type: TagReleaseTask)

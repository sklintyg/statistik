plugins {
    id "io.spring.dependency-management" version "1.1.6" apply false
    id "se.inera.intyg.plugin.fitnesse" version "1.2.1" apply false
    id "com.netflix.nebula.node" version "3.0.0" apply false
    id "org.gretty" version "4.1.5" apply false
    id 'test-report-aggregation'
    id 'org.sonarqube' version '5.1.0.4882'
    id "org.owasp.dependencycheck" version "10.0.4"
    id 'jacoco'
}

ext {
    infraVersion = System.properties['infraVersion'] ?: '4.0.0-SNAPSHOT'

    intygSchematronVersion = "1.0.57.2"
    rivtaClinicalprocessSchemasVersion = "4.0.5.3"
    internalMonitoringSchemaVersion = "1.0.4.3"
    intygTransportstyrelsenSchemasVersion = "1.0.5.3"

    auth0JwksVersion = "0.22.1"
    commonsIoVersion = "2.14.0"
    commonsLoggingVersion = "1.3.4"
    commonsTextVersion = "1.12.0"
    cxfVersion = "4.0.5"
    fitnesseVersion = "20240707"
    gatlingVersion = "3.9.5" // High chars - handled by K1J-414
    jakartaMessagingApiVersion = "3.1.0"
    googleGuavaVersion = "33.0.0-jre"
    gparsVersion = "1.2.1"
    groovyAllVersion = "4.0.23"
    groovyHttpVersion = "0.7.1"
    jsonwebtokenVersion = "0.11.5"
    picocliVersion = "4.7.6"
    poiVersion = "5.3.0"
    schematronVersion = "5.6.5"
    springSecuritySamlVersion = "1.0.3.RELEASE"
    zipletVersion = "2.4.1"
    shedlockSpringVersion = "5.16.0"
    wagonVersion = "3.5.3"

    nodeJsVersion = "12.22.12"
    npmVersion = "6.14.16"

    springbootMavenBomVersion = "3.3.4"

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

allprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = 'se.inera.statistik'
    version = System.properties['buildVersion'] ?: '4.0.0-SNAPSHOT'
    sourceCompatibility = 21
    targetCompatibility = 21

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    dependencies {
        implementation "ch.qos.logback:logback-classic"
        implementation "com.google.guava:guava:${googleGuavaVersion}"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.mockito:mockito-core"
        testImplementation "org.springframework:spring-test"

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    tasks.withType(JavaCompile).configureEach { options.encoding = 'UTF-8' }

    compileJava.dependsOn processResources
    test { exclude '**/*FunctionalTest.*' }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }

    // Spotbugs does not work very well with groovy code. Hence we exclude groovy projects.
    // Errorprone has no mechanism (yet) for excluding generated code. We therefore disable it for hsa-integration.
    ext.spotbugsExclude = "(fileservice|.*groovy)\$"
    ext.errorproneExclude = "hsa-integration"
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-rehabstod"
        property "sonar.projectName", "intyg-rehabstod"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}

dependencyCheck {
    formats = List.of("HTML", "JSON")
    nvd.setDatafeedUrl(nvdMirror)
    analyzers.setAssemblyEnabled(false)
}

reporting {
    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}

apply from: "checkPnr.gradle"

configure(subprojects.findAll { ['statistik-service', 'web'].contains(it.name) }) {
    test { exclude '**/*IT*' }
    tasks.register('integrationTest', Test) {
        outputs.upToDateWhen { false }
        include '**/*IT*'
    }
}
description = 'Inera Statistics Fitnesse specifications'

apply plugin: 'groovy'

ext.fitnessePort = "9125"
ext.fitnesseRoot = "FitNesseRoot"
ext.fitnesseDir = "src/test/fitnesse"

class FitnesseTask extends DefaultTask {
    def clArgs = ['-p', project.fitnessePort, '-d', project.fitnesseDir, '-r', project.fitnesseRoot, '-o']

    @TaskAction
    def runFitnesse() {
        project.javaexec {
            main = "fitnesse.FitNesse"
            classpath = project.configurations.runtime
            systemProperties(["maven.classpath": mavenPathAsWikiPaths()])
            args = clArgs.toList()
        }
    }

    def mavenPathAsWikiPaths() {
        // All jars built in this project plus dependency jars
        (project.configurations.archives.artifacts.files + project.configurations.runtime.asFileTree).collect { file ->
            "!path ${file.path}"
        }.join("\n")
    }
}

task fitnesseWiki(type: FitnesseTask, dependsOn: build)

task fitnesseTest(type: FitnesseTask, dependsOn: build) {
    clArgs += ['-c', 'StatisticsTests?suite&format=text']
}

dependencies {
    compile project(":statistik-web")

    compile "org.fitnesse:fitnesse:20140901"
    compile "org.codehaus.groovy:groovy-all:2.3.6"
    compile "org.codehaus.groovy.modules.http-builder:http-builder:0.7"
}

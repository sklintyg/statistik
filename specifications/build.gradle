apply plugin: 'groovy'
apply plugin: 'inera-fitnesse'
apply plugin: 'com.moowork.grunt'

node {
    version = '6.6.0'
    download = true
    distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
    npmVersion = '3.10.8'
    workDir = file("${project.buildDir}/nodejs")
    nodeModulesDir = file("${project.projectDir}")
}

task setupIntygForProtractor(dependsOn: 'classes', type: JavaExec) {

    def baseUrl = System.properties['statistics.base.url'] ?: "http://localhost:8080/"

    jvmArgs = ['-Dstatistics.base.url=' + baseUrl]

    main = 'se.inera.statistics.InsertIntygHelper'
    classpath = sourceSets.main.runtimeClasspath
}

task protractorTests(type: NodeTask, dependsOn: [npmInstall, setupIntygForProtractor]) {
    outputs.upToDateWhen { false }
    def environment = System.properties['protractor.env'] ?: "dev"

    script = file('node_modules/grunt-cli/bin/grunt')
    args = ["default:${environment}"]
}

fitnesse {
    port = 9125
    root = "FitNesseRoot"
    workingDir = "src/test/fitnesse"
    wikiStartPage = "StatisticsTests"

    if (project.hasProperty('fileOutput')) {
        outputFormat = project.findProperty('outputFormat') ?: "xml&includehtml"
    }

    extraProperties = ["statistics.base.url": System.properties['statistics.base.url'] ?: "http://localhost:8080/" ]
}

task allIntegrationTests(dependsOn: [fitnesseTest]) {
    doLast {
        println 'running integration tests'
    }
}

if (project.hasProperty('runFitnesse')) {
    apply plugin: 'org.akhikhl.gretty'
    gretty {
        def baseUrl = 'http://localhost:8080'

        if (project.hasProperty('runWithIntyg')) {
            httpPort = 9101
            baseUrl = 'http://localhost:9101'
        }

        contextPath = '/'
        contextConfigFile = project(":statistik-web").projectDir.path + "/src/main/webapp/WEB-INF/jetty-context.xml"
        jvmArgs = ["-Dcertificate.logback.file=${projectDir}/src/main/resources/logback-test.xml",
                   '-Dstatistics.base.url=' + baseUrl,
                   '-Dspring.profiles.active=dev,testapi,embedded,hsa-stub,security-fake,skipTestIntygInjection',
                   '-Dstatistics.config.file=' + project(":statistik-web").projectDir.path + '/src/main/resources/default.dev.properties',
                   '-Dstatistics.credentials.file=' + project(":statistik-web").projectDir.path + '/src/main/resources/dev-credentials.properties']
    }

    farms {
        farm 'Fitnesse', {
            webapp ":statistik-web"
            webapp project
            if (project.hasProperty('runFitnesse') && project.hasProperty('runProtractor')) {
                integrationTestTask = 'allIntegrationTests'
            } else if (project.hasProperty('runFitnesse')) {
                integrationTestTask = 'fitnesseTest'
            } else {
                integrationTestTask = 'protractorTest'
            }
        }
    }

    dependencies {
        gretty "com.h2database:h2:${h2Version}"
        gretty "mysql:mysql-connector-java:${mysqlConnectorVersion}"
        gretty "org.apache.activemq:activemq-broker:${activeMqVersion}"
        gretty "org.apache.tomcat:tomcat-catalina:${tomcatVersion}"
    }
}

clean {
    delete "${projectDir}/src/test/fitnesse/FitNesseRoot/ErrorLogs"
    delete "${projectDir}/src/test/fitnesse/FitNesseRoot/files/testResults"
    delete "${projectDir}/catalina.base_IS_UNDEFINED"
}

dependencies {
    compile project(":statistik-web")

    compile "org.fitnesse:fitnesse:${fitnesseVersion}"
    compile "org.codehaus.groovy:groovy-all:${groovyAllVersion}"
    compile "org.codehaus.groovy.modules.http-builder:http-builder:${groovyHttpVersion}"
    compile "org.apache.httpcomponents:httpmime:${httpVersion}"
    compile "org.apache.httpcomponents:httpcore:${httpVersion}"

    fitnesse(project(":statistik-web")) //{ transitive = false }
    //fitnesse(project(":statistik-service")) { transitive = false }
    //fitnesse(project(":specifications")) { transitive = false }

    fitnesse "org.fitnesse:fitnesse:${fitnesseVersion}"
    fitnesse "org.codehaus.groovy:groovy-all:${groovyAllVersion}"
    fitnesse "org.codehaus.groovy.modules.http-builder:http-builder:${groovyHttpVersion}"
    fitnesse "org.apache.httpcomponents:httpmime:${httpVersion}"
    fitnesse "org.apache.httpcomponents:httpcore:${httpVersion}"
    fitnesse "org.slf4j:slf4j-api:${slf4jVersion}"
    fitnesse "javax.ws.rs:javax.ws.rs-api:${javaxWsRsApiVersion}"
}

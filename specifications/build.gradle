buildscript {
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.1.8'
        classpath 'se.inera.intyg:gradle-fitnesse-plugin:1.0-SNAPSHOT'
    }
}

plugins {
    id "com.moowork.grunt" version "0.13"
    id "com.moowork.node" version "0.13"
}

description = 'Inera Statistics Fitnesse specifications'

apply plugin: 'groovy'
apply plugin: 'inera-fitnesse'


node {
    version = '6.6.0'
    download = true
    distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
    npmVersion = '3.10.8'
    workDir = file("${project.buildDir}/nodejs")
    nodeModulesDir = file("${project.projectDir}")
}

String nodeBinaryPath = new com.moowork.gradle.node.variant.VariantBuilder(node).build().nodeExec

fitnesse {
    port = 9125
    root = "FitNesseRoot"
    workingDir = "src/test/fitnesse"
}

npm_install.dependsOn(npm_cache_clean)

task setupIntygForProtractor (dependsOn: 'classes', type: JavaExec) {
    main = 'se.inera.statistics.InsertIntygHelper'
    classpath = sourceSets.main.runtimeClasspath
}

task protractorTest(dependsOn: [npmInstall, setupIntygForProtractor], type: Exec) {
    commandLine nodeBinaryPath, './node_modules/protractor/bin/protractor', 'src/test/protractor/protractor.conf.js'
}

task allIntegrationTests(dependsOn: [fitnesseTest, protractorTest]) << {
    println 'running integration tests'
}

if (project.hasProperty('runFitnesse') || project.hasProperty('runProtractor')) {
    apply plugin: 'org.akhikhl.gretty'
    gretty {
        contextPath = '/'
        contextConfigFile = project(":statistik-web").projectDir.path + "/src/main/webapp/WEB-INF/jetty-context.xml"
        logbackConfigFile = "${projectDir}/src/main/resources/logback-test.xml"
        jvmArgs = [
                '-Dspring.profiles.active=dev,embedded,hsa-stub,security-fake,skipTestIntygInjection',
                '-Dstatistics.config.file=' + project(":statistik-web").projectDir.path + '/src/main/resources/default.dev.properties',
                '-Dstatistics.credentials.file=' + project(":statistik-web").projectDir.path + '/src/main/resources/dev-credentials.properties'
        ]
//        jvmArgs = ['-Dspring.profiles.active=dev,embedded,hsa-stub,security-fake', '-Dstatistics.test.max.intyg=1', '-XX:MaxPermSize=256M']
    }

    farm {
        webapp ":statistik-web"
        webapp project
        if (project.hasProperty('runFitnesse') && project.hasProperty('runProtractor')) {
            integrationTestTask = 'allIntegrationTests'
        } else if (project.hasProperty('runFitnesse')) {
            integrationTestTask = 'fitnesseTest'
        } else {
            integrationTestTask = 'protractorTest'
        }
    }

    dependencies {
        gretty "com.h2database:h2:${h2Version}"
        gretty "mysql:mysql-connector-java:${mysqlConnectorVersion}"
        gretty "org.apache.activemq:activemq-broker:${activeMqVersion}"
        gretty "org.apache.tomcat:tomcat-catalina:${tomcatVersion}"
     }
}

clean {
    delete "${projectDir}/src/test/fitnesse/FitNesseRoot/ErrorLogs"
    delete "${projectDir}/src/test/fitnesse/FitNesseRoot/files/testResults"
    delete "${projectDir}/catalina.base_IS_UNDEFINED"
}

dependencies {
    compile project(":statistik-web")

    compile "org.fitnesse:fitnesse:${fitnessVersion}"
    compile "org.codehaus.groovy:groovy-all:2.4.0"
    compile "org.codehaus.groovy.modules.http-builder:http-builder:${groovyHttpVersion}"
    compile "org.apache.httpcomponents:httpmime:${httpVersion}"
    compile "org.apache.httpcomponents:httpcore:${httpVersion}"
}

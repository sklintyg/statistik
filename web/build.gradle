apply plugin: 'com.netflix.nebula.grunt'
apply plugin: 'org.gretty'
apply plugin: 'idea'
apply plugin: 'war'

gretty {
    def instance = System.properties['instance'] ?: '0'
    def useHttpPort = 8050 + (instance as Integer)
    def useInternalHttpPort = 8150 + (instance as Integer)
    def applicationDir = "${rootProject.projectDir}/devops/dev"
    def baseUrl = "http://localhost:${useHttpPort}"

    servletContainer = 'tomcat9'
    contextPath = '/'
    scanInterval = 0
    debugPort = 8850 + (instance as Integer)
    portPropertiesFileName = "gretty_ports_${instance}.properties"
    debugSuspend = false
    fastReload = true
    inplaceMode = 'hard'
    recompileOnSourceChange = false
    reloadOnClassChange = false
    reloadOnConfigChange = false
    reloadOnLibChange = false
    serverConfigFile = "tomcat-gretty.xml"

    jvmArgs = ["-Dcatalina.base=${project.layout.buildDirectory.get().asFile}/catalina.base",
               "-Dspring.profiles.active=dev,caching-enabled,hsa-caching-disabled,testapi,hsa-stub,wc-hsa-stub,security-both,noprocessing,ia-stub,request-caching-enabled",
               "-Dlogback.file=${applicationDir}/config/logback-dev.xml",
               "-Djava.awt.headless=true",
               "-Dfile.encoding=UTF-8",
               "-Dstatistics.test.max.intyg=200",
               "-DbaseUrl=http://localhost:${useHttpPort}",
               "-Dapplication.dir=${applicationDir}",
               "-Ddev.config.file=${applicationDir}/config/application-dev.properties",
               "-Ddev.http.port=${useHttpPort}",
               "-Ddev.http.port.internal=${useInternalHttpPort}"
    ]

    dependencies {
        gretty "jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api"
    }
}

idea {
    module {
        excludeDirs += file('node_modules')
        excludeDirs += file('src/main/webapp/bower_components')
    }
}

node {
    version = "${nodeJsVersion}"
    npmVersion = "${npmVersion}"
    download = true
    distBaseUrl = 'https://nodejs.org/dist/'
    workDir = file("${rootProject.projectDir}/.gradle/nodejs")
    npmWorkDir = file("${rootProject.projectDir}/.gradle/npm")
    nodeModulesDir = file("${project.projectDir}")
}

if (project.hasProperty('useMinifiedJavaScript')) {
    war {
        webAppDirName = 'build/webapp'
    }
    war.dependsOn grunt_build
} else if (project.hasProperty('useNoMinifiedJavaScriptNoKarma')) {
    war.dependsOn grunt_build_no_minify_no_karma
} else {
    war.dependsOn grunt_build_no_minify
}

grunt { colors = true }
grunt_build.dependsOn npmInstall
grunt_build_no_minify.dependsOn npmInstall
grunt_build_no_minify_no_karma.dependsOn npmInstall
war.dependsOn createVersionPropertyFile

dependencies {
    implementation project(':hsa-integration')
    implementation project(':statistik-service')

    implementation "se.riv.clinicalprocess.healthcond.monitoring:clinicalprocess-healthcond-monitoring-schemas:${internalMonitoringSchemaVersion}"

    implementation "se.inera.intyg.infra:driftbanner-dto:${infraVersion}"
    implementation "se.inera.intyg.infra:dynamiclink:${infraVersion}"
    implementation "se.inera.intyg.infra:hsatk-integration:${infraVersion}"
    implementation "se.inera.intyg.infra:ia-integration:${infraVersion}"
    implementation "se.inera.intyg.infra:monitoring:${infraVersion}"
    implementation "se.inera.intyg.infra:security-filter:${infraVersion}"

    implementation "com.auth0:jwks-rsa:${auth0JwksVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "io.jsonwebtoken:jjwt-impl:${jsonwebtokenVersion}"
    implementation "io.jsonwebtoken:jjwt-jackson:${jsonwebtokenVersion}"
    implementation "io.prometheus:simpleclient_servlet"
    implementation "net.javacrumbs.shedlock:shedlock-spring:${shedlockSpringVersion}"
    implementation "org.apache.commons:commons-text:${commonsTextVersion}"
    implementation "org.apache.cxf:cxf-rt-frontend-jaxrs:${cxfVersion}"
    implementation "org.apache.poi:poi-ooxml:${poiVersion}"
    implementation "org.liquibase:liquibase-core"
    implementation "org.springframework.security.extensions:spring-security-saml2-core:${springSecuritySamlVersion}"
    implementation "org.springframework:spring-jms"

    compileOnly "jakarta.persistence:jakarta.persistence-api"
    compileOnly "jakarta.ws.rs:jakarta.ws.rs-api"

    runtimeOnly "com.github.ziplet:ziplet:${zipletVersion}"
    runtimeOnly "com.mysql:mysql-connector-j"
    runtimeOnly "org.apache.activemq:activemq-spring"
    runtimeOnly "se.inera.intyg.infra:hsa-integration-intyg-proxy-service:${infraVersion}"

    testImplementation "jakarta.transaction:jakarta.transaction-api"
    testCompileOnly "jakarta.persistence:jakarta.persistence-api"
}

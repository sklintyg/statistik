buildscript {
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.1.7'
    }
}

plugins {
    id "com.moowork.node" version "0.7"
}

apply plugin: "war"
apply plugin: 'org.akhikhl.gretty'

description = 'Inera Statistics Service - Web Module'

gretty {
    contextPath = '/'
    // TODO kan troligen ta bort explicit konfning om vi flyttar filen
    contextConfigFile = "${projectDir}/src/main/webapp/WEB-INF/jetty-context.xml"
    logbackConfigFile = "${projectDir}/src/main/resources/statistics-logback.xml"
    jvmArg '-Dspring.profiles.active=dev,embedded'
}

node {
    version = '0.10.32'
    npmVersion = '2.0.0'
}

task jsTests(type: NodeTask) {
    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['test']
}

test.dependsOn(jsTests)

// TODO: möjligen ersätta med Copy task och filtrering
task createVersionPropertyFile(dependsOn: processResources) << {
    def propertyFile = "${buildDir}/resources/main/version.properties"
    ant.delete(file: propertyFile)
    ant.touch(file: propertyFile, mkdirs: "true")
    ant.propertyfile(file: propertyFile) {
        entry(key: 'project.version', default: project.version)
    }
}

createVersionPropertyFile.mustRunAfter processResources
jar.dependsOn createVersionPropertyFile

dependencies {
    compile project(':statistik-service')

    compile "org.springframework.security.extensions:spring-security-saml2-core:1.0.0.RC2"
    compile "xml-apis:xml-apis:1.4.01"
    compile "org.springframework:spring-webmvc:$springContextVersion"
    compile "jstl:jstl:1.2"
    compile "org.apache.commons:commons-lang3:3.2.1"

    compile(group: "org.springframework.security", name: "spring-security-config", version: springSecurityVersion) {
        exclude(module: "spring-expression")
        exclude(module: "spring-aop")
        exclude(module: "spring-jdbc")
        exclude(module: "spring-core")
        exclude(module: "spring-beans")
        exclude(module: "spring-tx")
    }
    compile(group: "org.springframework.security", name: "spring-security-taglibs", version: springSecurityVersion) {
        exclude(module: "spring-expression")
        exclude(module: "spring-aop")
        exclude(module: "spring-jdbc")
        exclude(module: "spring-core")
        exclude(module: "spring-beans")
        exclude(module: "spring-tx")
    }

    gretty "com.h2database:h2:1.3.171"
    gretty "mysql:mysql-connector-java:5.1.29"
    gretty "org.apache.activemq:activemq-broker:5.8.0"
    gretty "org.apache.tomcat:tomcat-catalina:7.0.20"

    providedCompile "javax.servlet:servlet-api:2.5"
}
